plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.1.61"
}

// This is important even if Android Studio claims it isn't
// used. Android can't interpret Java 8 byte code.
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

// Configure Kotlin plugin for compatibility with 1.1
compileKotlin {
    kotlinOptions {
        apiVersion = "1.1"
        languageVersion = "1.1"
    }
}

ext {
    kotlinVersion = "1.1.61"

    // This params is for the library uploading to the Bintray
    bintrayRepo = 'terramaven'
    bintrayName = 'cicerone'
    publishedGroupId = 'ru.terrakok.cicerone'
    artifact = 'cicerone'
    libraryVersion = '2.1.0'
    gitUrl = 'https://github.com/terrakok/Cicerone'
    allLicenses = ['MIT']
}

dependencies {
    compileOnly project(':stub-android')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
}

// Configuration of the library uploading to the Bintray
// Note: Call 'bintrayUpload' task (it will execute 'install' task first)
// I try to include as little properties as possible in these files
project.archivesBaseName ='cicerone' // to fix that project name different from artifact name
apply from: 'androidmaven.gradle'
apply from: 'bintray.gradle'

// Tasks for sources and javadocs jars
task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
