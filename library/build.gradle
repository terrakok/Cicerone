apply plugin: 'kotlin'
apply plugin: 'maven-publish'

// This is important even if Android Studio claims it isn't
// used. Android can't interpret Java 8 byte code.
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compileOnly project(':stub-android')
    compileOnly 'com.google.android:android:4.1.1.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

ext {
    // This params is for the library uploading to the Bintray
    bintrayRepo = 'terramaven'
    bintrayName = 'cicerone-kotlin'
    publishedGroupId = 'com.github.terrakok'
    artifact = 'cicerone'
    libraryVersion = '6.4'
    gitUrl = 'https://github.com/terrakok/Cicerone'
    allLicenses = ['MIT']
}

// Configuration of the library uploading to the Bintray
// Note: Call 'bintrayUpload' task (it will execute 'install' task first)
// I try to include as little properties as possible in these files
project.archivesBaseName = 'cicerone' // to fix that project name different from artifact name
apply from: 'androidmaven.gradle'
apply from: 'bintray.gradle'

// Tasks for sources and javadocs jars
task sourcesJar(type: Jar) {
    from sourceSets.main.kotlin.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = publishedGroupId
            artifactId = artifact
            version = libraryVersion

            artifact sourcesJar
            artifact javadocJar
            from components.java
        }
    }
}